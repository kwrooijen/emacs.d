** Mode line faces

#+BEGIN_SRC emacs-lisp
(defface kwrooijen/modeline-border
  `((t (:foreground ,(face-attribute 'kwrooijen/foreground-fg-face :foreground)))
    (t (:background ,(face-attribute 'kwrooijen/darken-fg-face :background)))
    (t (:height 1.5)))
  "Face for the active window border."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-border-inactive
  `((t (:foreground ,(face-attribute 'kwrooijen/darken-fg-face :foreground)))
    (t (:background ,(face-attribute 'kwrooijen/darken-fg-face :background)))
    (t (:height 1.5)))
  "Face for the inactive window border."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-evil
  '((t (:height 1.0)))
  "Face for evil state indicator."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-winum
  '((t (:height 1.0)))
  "Face for winum number."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-buffer
  '((t (:height 1.0)))
  "Face for buffer/file name."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-scroll
  '((t (:height 1.0)))
  "Face for scroll percentage indicator."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-git-branch
  '((t (:height 1.0
        :inherit success)))
  "Face for Git branch."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-major-mode
  '((t (:height 1.0)))
  "Face for major mode text."
  :group 'kwrooijen/modeline)

(kwrooijen/reset-inherit-face!
 'mode-line           'kwrooijen/darken-bg-face
 'mode-line-active    'kwrooijen/darken-bg-face
 'mode-line-inactive  'kwrooijen/darken-bg-face
 'mode-line-highlight 'unspecified)

(set-face-attribute 'kwrooijen/modeline-border nil :height 'unspecified :inherit 'default)
(set-face-attribute 'kwrooijen/modeline-border-inactive nil :height 1.5 :inherit 'kwrooijen/darken-fg-face)
#+END_SRC

** Active bar

#+BEGIN_SRC emacs-lisp
(defvar kwrooijen/modeline-raise 0.15)

(defun kwrooijen/modeline-r (s face)
  "Apply face and vertical raise to a modeline string."
  (propertize s 'face face 'display `(raise ,kwrooijen/modeline-raise)))

(defvar kwrooijen/modeline-active-border "▎")
(defvar kwrooijen/modeline-inactive-border "▎")
(defvar kwrooijen/modeline-selected-window nil)

(defun kwrooijen/modeline-win-focus-border (&optional force)
  "Show a border only when the window is active."
(set-face-attribute  'kwrooijen/modeline-border nil
        :foreground (face-attribute 'kwrooijen/foreground-fg-face :foreground)
        :background (face-attribute 'kwrooijen/darken-bg-face :background)
        :height 1.5)
(set-face-attribute  'kwrooijen/modeline-border-inactive nil
        :foreground (face-attribute 'kwrooijen/darken-fg-face :foreground)
        :background (face-attribute 'kwrooijen/darken-fg-face :background)
        :height 1.5)
  (if (or force (eq (selected-window) kwrooijen/modeline-selected-window))
      (propertize kwrooijen/modeline-active-border 'face 'kwrooijen/modeline-border)
    (propertize kwrooijen/modeline-inactive-border 'face 'kwrooijen/modeline-border-inactive)))

(defun kwrooijen/modeline--set-selected-window (&rest _)
  (unless (minibuffer-window-active-p (selected-window))
    (setq kwrooijen/modeline-selected-window (selected-window))))

(add-hook 'post-command-hook #'kwrooijen/modeline--set-selected-window)
#+END_SRC

** Segments

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-evil-tag ()
  (when (bound-and-true-p evil-local-mode)
    (let* ((state evil-state)
           (tag
            (upcase
             (pcase state
               ('normal  " N ")
               ('insert  " I ")
               ('visual  " V ")
               ('replace " R ")
               (_ (substring (symbol-name state) 0 1)))))
           (face
            (pcase state
              ('normal  'success)
              ('visual  'region)
              ('insert  'error)
              ('replace 'warning)
              (_ 'kwrooijen/modeline-evil))))
      (kwrooijen/modeline-r tag face))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-winum ()
  (when (bound-and-true-p winum-mode)
    (let* ((num-str (format "%d " (or (winum-get-number) 1)))
           (face (if (eq (selected-window) kwrooijen/modeline-selected-window)
                     `(:inherit kwrooijen/modeline-winum :weight bold)
                   'kwrooijen/modeline-winum)))
      (kwrooijen/modeline-r num-str face))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-git-branch ()
  ""
  (when-let ((branch (and (fboundp 'magit-get-current-branch)
                          (magit-get-current-branch))))
    (kwrooijen/modeline-r
     (format "   %s" branch)
     'kwrooijen/modeline-git-branch)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-scroll ()
  (kwrooijen/modeline-r " %p%% " 'kwrooijen/modeline-scroll))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-line-col ()
  (kwrooijen/modeline-r " %l:%c" 'kwrooijen/modeline-scroll))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-buffer-name ()
  (let* ((dirty? (buffer-modified-p))
         (face   (if dirty?
                     `(:foreground ,(face-attribute 'kwrooijen/error-fg-face :foreground))
                   'kwrooijen/modeline-buffer)))
    (kwrooijen/modeline-r " %b" face)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/weekday-p (&optional time)
  "Return t if TIME (or now) is Monday through Friday."
  (let ((day-of-week (nth 6 (decode-time time))))
    (<= 1 day-of-week 5)))

(defun kwrooijen/clocked-today ()
  (let ((clocked-today (kwrooijen/org-clock-clocked-today)))
    (format "%d:%02d"
            (/ (floor clocked-today) 60)
            (% (floor clocked-today) 60))))

(defun kwrooijen/clocked-today-percentage ()
  (format "%d％"
          (round
           (* 100.0
              (/ (float (kwrooijen/org-clock-clocked-today))
                 480.0)))))


(defun kwrooijen/clocked-session ()
  (format "%d:%02d"
          (/ (org-clock-get-clocked-time) 60)
          (% (org-clock-get-clocked-time) 60)))

(defun kwrooijen/modeline-org-clock ()
  (when (kwrooijen/weekday-p)
    (let* ((clock-count (length org-clock-multi-clocks))
           (clocked-in-p (> clock-count 0))
           (face (if clocked-in-p 'kwrooijen/warning-fg-face 'kwrooijen/faded-fg-face)))
      (kwrooijen/modeline-r (format " [%d | %s | %s]" clock-count (kwrooijen/clocked-today) (kwrooijen/clocked-today-percentage)) face))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-mode ()
  (let* ((active? (eq (selected-window) kwrooijen/modeline-selected-window))
         (mode-face (if active?
                        `(:inherit kwrooijen/modeline-major-mode :weight bold)
                      'kwrooijen/modeline-major-mode)))
    (kwrooijen/modeline-r "  %m  " mode-face)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-zoom ()
  (let* ((zoom (/ (- (face-attribute 'default :height)
                     kwrooijen/default-font-size) kwrooijen/font-step-size))
         (affix (if (> zoom 0) "+" "-")))
    (unless (equal zoom 0)
      (kwrooijen/modeline-r (format " %s%s" zoom affix)
                            'kwrooijen/popout-fg-face))))
#+END_SRC


** Build modeline

Center spacing

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-center-space (right)
  (propertize
   " "
   'display
   `((space :align-to
            (- (+ right right-fringe right-margin)
               ,(string-width (format-mode-line right)))))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defcustom kwrooijen/modeline-left
  (list
   '(:eval (kwrooijen/modeline-win-focus-border))
   '(:eval (kwrooijen/modeline-winum))
   '(:eval (kwrooijen/modeline-evil-tag))
   '(:eval (kwrooijen/modeline-buffer-name))
   '(:eval (kwrooijen/modeline-line-col))
   '(:eval (kwrooijen/modeline-scroll)))
  "Left side of the modeline."
  :type 'list
  :group 'kwrooijen/modeline)

(defcustom kwrooijen/modeline-right
  (list
   '(:eval (when (fboundp 'kwrooijen/modeline-agent-shell) (kwrooijen/modeline-agent-shell)))
   '(:eval (kwrooijen/modeline-org-clock))
   '(:eval (kwrooijen/modeline-zoom))
   '(:eval (kwrooijen/modeline-git-branch))
   '(:eval (kwrooijen/modeline-mode)))
  "Right side of the modeline."
  :type 'list
  :group 'kwrooijen/modeline)

(defun kwrooijen/mode-line ()
  (let* ((left  (delq nil kwrooijen/modeline-left))
         (right (delq nil kwrooijen/modeline-right)))
    (concat
     (format-mode-line left)
     (kwrooijen/modeline-center-space right)
     (format-mode-line right))))

(setq-default mode-line-format
              '((:eval (kwrooijen/mode-line))))

(when (buffer-live-p (get-buffer "*Messages*"))
  (with-current-buffer "*Messages*"
    (setq mode-line-format
          '((:eval (kwrooijen/mode-line))))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; TODO implement project relative buffer name (with trimming)
(defun kwrooijen/buffer-path-from-project-root ()
  "Return path starting with the project root directory name.
If not a file buffer, return the buffer name."
  (interactive)
  (let* ((fname (buffer-file-name))
         (proj-root (when (fboundp 'kwrooijen/project-root)
                      (kwrooijen/project-root))))
    (cond
     ((and fname proj-root)
      (concat (file-name-nondirectory
               (directory-file-name proj-root))
              "/"
              (file-relative-name fname proj-root)))
     (fname (file-name-nondirectory fname))
     (t (buffer-name)))))
#+END_SRC
