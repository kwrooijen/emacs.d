** Mode line faces

#+BEGIN_SRC emacs-lisp
(defface kwrooijen/modeline-border
  '()
  "Face for the active window border."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-border-inactive
  `((t (:foreground ,(face-attribute 'kwrooijen/darken-fg-face :foreground)))
    (t (:background ,(face-attribute 'kwrooijen/darken-fg-face :background)))
    (t (:height 1.5)))
  "Face for the inactive window border."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-evil
  '((t (:height 1.0)))
  "Face for evil state indicator."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-winum
  '((t (:height 1.0)))
  "Face for winum number."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-buffer
  '((t (:height 1.0)))
  "Face for buffer/file name."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-scroll
  '((t (:height 1.0)))
  "Face for scroll percentage indicator."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-git-branch
  '((t (:height 1.0
        :inherit success)))
  "Face for Git branch."
  :group 'kwrooijen/modeline)

(defface kwrooijen/modeline-major-mode
  '((t (:height 1.0)))
  "Face for major mode text."
  :group 'kwrooijen/modeline)

(kwrooijen/reset-inherit-face!
 'mode-line           'kwrooijen/darken-bg-face
 'mode-line-active    'kwrooijen/darken-bg-face
 'mode-line-inactive  'kwrooijen/darken-bg-face
 'mode-line-highlight 'unspecified)

(set-face-attribute 'kwrooijen/modeline-border nil :height 1.5)
(set-face-attribute 'kwrooijen/modeline-border-inactive nil :height 1.5)
#+END_SRC

** Active bar

#+BEGIN_SRC emacs-lisp
(defvar kwrooijen/modeline-raise 0.20)

(defun kwrooijen/modeline-r (s face)
  "Apply face and vertical raise to a modeline string."
  (propertize s 'face face 'display `(raise ,kwrooijen/modeline-raise)))

(defvar kwrooijen/modeline-active-border "▎")
(defvar kwrooijen/modeline-inactive-border "▎")

(defun kwrooijen/modeline-win-focus-border (&optional force)
  "Show a border only when the window is active."
  (if (or force (eq (selected-window) kwrooijen/modeline-selected-window))
      (propertize kwrooijen/modeline-active-border 'face 'kwrooijen/modeline-border)
    (propertize kwrooijen/modeline-inactive-border 'face
                `(:foreground ,(face-attribute 'kwrooijen/modeline-border-inactive :foreground)
                  :height ,(face-attribute 'kwrooijen/modeline-border-inactive :height)))))

(defun kwrooijen/modeline--set-selected-window (&rest _)
  (unless (minibuffer-window-active-p (selected-window))
    (setq kwrooijen/modeline-selected-window (selected-window))))

(add-hook 'post-command-hook #'kwrooijen/modeline--set-selected-window)
#+END_SRC

** Segments

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-evil-tag ()
  (when (bound-and-true-p evil-local-mode)
    (let* ((state evil-state)
           (tag
            (upcase
             (pcase state
               ('normal  "  N ")
               ('insert  "  I ")
               ('visual  "  V ")
               ('replace "  R ")
               (_ (substring (symbol-name state) 0 1)))))
           (face
            (pcase state
              ('normal  'success)
              ('visual  'region)
              ('insert  'warning)
              ('replace 'error)
              (_ 'kwrooijen/modeline-evil))))
      (kwrooijen/modeline-r tag face))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-winum ()
  (when (bound-and-true-p winum-mode)
    (let* ((num-str (format "%d" (or (winum-get-number) 1)))
           (face (if (eq (selected-window) kwrooijen/modeline-selected-window)
                     `(:inherit kwrooijen/modeline-winum :weight bold)
                   'kwrooijen/modeline-winum)))
      (kwrooijen/modeline-r num-str face))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-git-branch ()
  ""
  (when-let ((branch (magit-get-current-branch)))
    (kwrooijen/modeline-r
     (format "   %s" branch)
     'kwrooijen/modeline-git-branch)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-scroll ()
  (kwrooijen/modeline-r
   (cond
    ((eq (point) (point-min)) "Top")
    ((eq (point) (point-max)) "Bot")
    (t (format " %d％"
               (/ (* 100 (1- (line-number-at-pos)))
                  (count-lines (point-min) (point-max))))))
   'kwrooijen/modeline-scroll))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-line-col ()
  (kwrooijen/modeline-r
   (format " %d:%d"
           (line-number-at-pos)
           (current-column))
   'kwrooijen/modeline-scroll))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-buffer-name ()
  (let* ((dirty? (buffer-modified-p))
         (face   (if dirty?
                     `(:foreground ,(face-attribute 'kwrooijen/warning-fg-face :foreground))
                   'kwrooijen/modeline-buffer)))
    (kwrooijen/modeline-r " %b" face)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-org-clock ()
  (when (and (org-clocking-p)
           (org-clock-get-clocked-time))
      (kwrooijen/modeline-r
       (format " [%d:%02d]"
               (/ (org-clock-get-clocked-time) 60)
               (% (org-clock-get-clocked-time) 60))
       'kwrooijen/faded-fg-face)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-mode ()
  (let* ((active? (eq (selected-window) kwrooijen/modeline-selected-window))
         (mode-face (if active?
                        `(:inherit kwrooijen/modeline-major-mode :weight bold)
                      'kwrooijen/modeline-major-mode)))
    (kwrooijen/modeline-r "  %m  " mode-face)))
#+END_SRC

** Build modeline

Center spacing

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-center-space (right)
  (propertize
   " "
   'display
   `((space :align-to
            (- (+ right right-fringe right-margin)
               ,(string-width (format-mode-line right)))))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defcustom kwrooijen/modeline-left
  (list
   '(:eval (kwrooijen/modeline-win-focus-border))
   '(:eval (kwrooijen/modeline-winum))
   '(:eval (kwrooijen/modeline-evil-tag))
   '(:eval (kwrooijen/modeline-buffer-name))
   '(:eval (kwrooijen/modeline-line-col))
   '(:eval (kwrooijen/modeline-scroll)))
  "Left side of the modeline."
  :type 'list
  :group 'kwrooijen/modeline)

(defcustom kwrooijen/modeline-right
  (list
   '(:eval (kwrooijen/modeline-org-clock))
   '(:eval (kwrooijen/modeline-git-branch))
   '(:eval (kwrooijen/modeline-mode)))
  "Right side of the modeline."
  :type 'list
  :group 'kwrooijen/modeline)

(defun kwrooijen/mode-line ()
  (let* ((left  (delq nil kwrooijen/modeline-left))
         (right (delq nil kwrooijen/modeline-right)))
    (concat
     (format-mode-line left)
     (kwrooijen/modeline-center-space right)
     (format-mode-line right))))

(setq-default mode-line-format
              '((:eval (kwrooijen/mode-line))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; TODO implement project relative buffer name (with trimming)
(defun kwrooijen/buffer-path-from-project-root ()
  "Return path starting with the project root directory name.
If not a file buffer, return the buffer name."
  (interactive)
  (let* ((fname (buffer-file-name))
         (proj-root (when (fboundp 'projectile-project-root)
                      (projectile-project-root))))
    (cond
     ;; File inside a Projectile project
     ((and fname proj-root)
      (concat (file-name-nondirectory
               (directory-file-name proj-root))
              "/"
              (file-relative-name fname proj-root)))

     ;; File not in a project: just the buffer file name (absolute)
     (fname (file-name-nondirectory fname))

     ;; Not a file buffer
     (t (buffer-name)))))
#+END_SRC
