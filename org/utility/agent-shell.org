* Agent Shell
:PROPERTIES:
:PACKAGE:  agent-shell
:STRAIGHT: t
:END:

** Custom :custom:
#+BEGIN_SRC emacs-lisp
(agent-shell-preferred-agent-config (agent-shell-anthropic-make-claude-code-config))
#+END_SRC

** General :general:
#+BEGIN_SRC emacs-lisp
(kwrooijen/leader
 "l l" 'agent-shell
 "l m" 'agent-shell-set-session-model)
#+END_SRC


* Agent Review
:PROPERTIES:
:PACKAGE:  agent-review
:STRAIGHT: (agent-review :host github :repo "nineluj/agent-review")
:END:

** Config :config:
#+BEGIN_SRC emacs-lisp
(evil-set-initial-state 'agent-review-mode 'normal)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/agent-review ()
  "Run agent-review with Claude Code as the default agent."
  (interactive)
  (agent-review (agent-shell-anthropic-make-claude-code-config)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'agent-review
  (general-define-key
   :states '(normal)
   :keymaps 'agent-review-mode-map
   "m" #'agent-review-mark
   "u" #'agent-review-unmark
   "r" #'agent-review-refresh
   "M" #'agent-review-mark-all
   "U" #'agent-review-unmark-all
   "c" #'agent-review-copy-issues
   "E" #'agent-review-send-to-agent-shell)

  (evil-define-key 'normal agent-review-mode-map (kbd "<tab>") #'agent-review-jump-to-issue))
#+END_SRC

** General :general:
#+BEGIN_SRC emacs-lisp
(kwrooijen/leader
 "l r" 'kwrooijen/agent-review)
#+END_SRC


* Agent Shell Manager
:PROPERTIES:
:PACKAGE:  agent-shell-manager
:STRAIGHT: (agent-shell-manager :host github :repo "jethrokuan/agent-shell-manager")
:END:

** Custom :custom:
#+BEGIN_SRC emacs-lisp
(agent-shell-manager-side nil)
#+END_SRC

** Config :config:

#+BEGIN_SRC emacs-lisp
(push '("*Agent-Shell Buffers*" :height 6 :position bottom :stick t :dedicated t) popwin:special-display-config)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(evil-set-initial-state 'agent-shell-manager-mode 'normal)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/agent-shell--active-project-dirs ()
  "Return list of project directories that have an active agent-shell."
  (let ((dirs nil))
    (dolist (buf (agent-shell-buffers))
      (when (buffer-live-p buf)
        (let ((name (buffer-name buf)))
          (when (string-match " Agent @ \\(.+\\)$" name)
            (push (file-truename (match-string 1 name)) dirs)))))
    dirs))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/agent-shell-start-in-project ()
  "Start agent-shell in a selected project and switch to it.
Projects that already have an active agent-shell are excluded from the list.
Uses Claude Code as the default agent."
  (interactive)
  (let* ((active-dirs (kwrooijen/agent-shell--active-project-dirs))
         (all-projects (project-known-project-roots))
         (available-projects
          (seq-filter (lambda (proj)
                        (not (member (file-truename proj) active-dirs)))
                      all-projects)))
    (if (null available-projects)
        (message "No projects available (all have active agent-shells)")
      (let* ((selected (completing-read "Start agent-shell in project: " available-projects nil t))
             (default-directory selected))
        (agent-shell-start :config (agent-shell-anthropic-make-claude-code-config))))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/agent-shell--count-by-status ()
  "Return (WORKING . TOTAL) count of agent-shell buffers.
WORKING counts agents that are actively working (not waiting for input).
TOTAL counts all live agent-shell buffers."
  (let ((working 0)
        (total 0))
    (dolist (buf (agent-shell-buffers))
      (when (buffer-live-p buf)
        (let ((status (agent-shell-manager--get-status buf)))
          (unless (string= status "killed")
            (setq total (1+ total))
            (when (string= status "working")
              (setq working (1+ working)))))))
    (cons working total)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/modeline-agent-shell ()
  "Return modeline segment showing agent-shell status as [WORKING/TOTAL]."
  (when (fboundp 'agent-shell-buffers)
    (let* ((counts (kwrooijen/agent-shell--count-by-status))
           (working (car counts))
           (total (cdr counts)))
      (when (> total 0)
        (let ((face (if (< working total)
                        'kwrooijen/popout-fg-face
                      'kwrooijen/faded-fg-face)))
          (kwrooijen/modeline-r (format " [%d/%d]" working total) face))))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/agent-shell-switch-agent ()
  "Switch to an agent-shell buffer using completing-read."
  (interactive)
  (let* ((buffers (seq-filter #'buffer-live-p (agent-shell-buffers)))
         (entries
          (mapcar (lambda (buf)
                    (let* ((name (buffer-name buf))
                           (status (agent-shell-manager--get-combined-status buf))
                           (dir (abbreviate-file-name
                                 (with-current-buffer buf default-directory))))
                      (list status dir name buf)))
                  buffers)))
    (if (null entries)
        (message "No agent-shell buffers")
      (let* ((max-status-width (apply #'max (mapcar (lambda (e) (string-width (nth 0 e))) entries)))
             (max-dir-width (apply #'max (mapcar (lambda (e) (length (nth 2 e))) entries)))
             (candidates
              (mapcar (lambda (entry)
                        (let* ((status (nth 0 entry))
                               (dir (nth 1 entry))
                               (name (nth 2 entry))
                               (buf (nth 3 entry))
                               (padded-status (string-pad status max-status-width))
                               (padded-name (string-pad name max-dir-width))
                               (label (concat padded-status
                                              "  "
                                              padded-name
                                              "  "
                                              (propertize dir 'face 'kwrooijen/faded-fg-face))))
                          (cons label buf)))
                      entries))
             (collection (lambda (str pred action)
                           (if (eq action 'metadata)
                               '(metadata (display-sort-function . identity)
                                          (cycle-sort-function . identity))
                             (complete-with-action action (mapcar #'car candidates) str pred))))
             (selected (completing-read "Switch to agent: " collection nil t))
             (buffer (cdr (assoc selected candidates))))
        (when buffer
          (pop-to-buffer buffer))))))
#+END_SRC

** General :general:

#+BEGIN_SRC emacs-lisp
(kwrooijen/leader
 "l s" 'agent-shell-manager-toggle
 "l p" 'kwrooijen/agent-shell-start-in-project
 "l b" 'kwrooijen/agent-shell-switch-agent)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'agent-shell-manager
  (general-define-key
   :states '(normal)
   :keymaps 'agent-shell-manager-mode-map
   (kbd "RET") #'agent-shell-manager-goto
   "r" #'agent-shell-manager-refresh
   "l" #'agent-shell-manager-view-traffic
   "L" #'agent-shell-manager-toggle-logging
   "p" #'kwrooijen/agent-shell-start-in-project
   "C-c C-c" #'agent-shell-manager-interrupt)

  (evil-define-key 'normal agent-shell-manager-mode-map (kbd "<tab>") #'agent-shell-manager-goto))
#+END_SRC
