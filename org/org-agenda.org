* Org Agenda
:PROPERTIES:
:PACKAGE:  org-agenda
:END:

** Config :config:

Automatically save any org buffers when making changes in org agenda.

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/agenda-org-save-all-org-buffers ()
  (when (equal major-mode 'org-agenda-mode)
    ;; TODO Inhibit messages
    (org-save-all-org-buffers))
  (when (equal major-mode 'org-mode)
    ;; TODO Inhibit messages
    (save-buffer)))

(add-hook 'org-after-todo-state-change-hook #'kwrooijen/agenda-org-save-all-org-buffers)
(add-hook 'org-clock-in-hook                #'kwrooijen/agenda-org-save-all-org-buffers)
(add-hook 'org-clock-out-hook               #'kwrooijen/agenda-org-save-all-org-buffers)

;; Set TODO state to IN PROGRESS when clocking in
(defun kwrooijen/org-clock-multi-set-in-progress ()
  "Set the heading at point to IN PROGRESS."
  (save-excursion
    (org-back-to-heading t)
    (unless (string= (org-get-todo-state) "IN PROGRESS")
      (org-todo "IN PROGRESS"))))

(advice-add 'org-clock-multi-clock-in :after #'kwrooijen/org-clock-multi-set-in-progress)

;; Save org buffers after org-clock-multi operations
(advice-add 'org-clock-multi-clock-in      :after #'org-save-all-org-buffers)
(advice-add 'org-clock-multi-clock-out     :after #'org-save-all-org-buffers)
(advice-add 'org-clock-multi-clock-out-all :after #'org-save-all-org-buffers)
(advice-add 'org-clock-multi-clock-cancel  :after #'org-save-all-org-buffers)
#+END_SRC




#+BEGIN_SRC emacs-lisp
(defun kwrooijen/org-clock-multi-skip-unless-clocked-or-paused ()
  "Skip function for agenda: skip unless heading is clocked or paused."
  (let ((dominated t))
    (let ((id (org-entry-get nil "CLOCK_MULTI_ID")))
      (when id
        (when (or (org-clock-multi--find-clock-by-key id)
                  (member id org-clock-multi-paused))
          (setq dominated nil))))
    (when dominated
      (org-end-of-subtree t))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(setq org-agenda-custom-commands
      '(("D" "Done tasks"
         todo "DONE")
        ("k" "Currently clocked"
         ((alltodo "" ((org-agenda-skip-function
                        #'kwrooijen/org-clock-multi-skip-unless-clocked-or-paused)))))))

(org-todo-dashboard-install)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/org-agenda-done ()
  (interactive)
  (org-agenda nil "D"))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(cl-defun kwrooijen/org-todo-select-and-change (&key from to tag)
  "Select a task in state FROM and change it to state TO.
When TAG is non-nil, only show tasks that have that tag."
  (let (entries max-tag-width max-closed-width)
    (dolist (file (org-agenda-files))
      (with-current-buffer (find-file-noselect file)
        (org-with-wide-buffer
         (goto-char (point-min))
         (while (re-search-forward org-heading-regexp nil t)
           (when (and (equal (org-get-todo-state) from)
                      (or (null tag)
                          (member tag (org-get-tags))))
             (let* ((heading (org-get-heading t t t t))
                    (tags (org-get-tags))
                    (tag-str (if tags (string-join tags ":") ""))
                    (closed (or (org-entry-get nil "CLOSED") ""))
                    (marker (point-marker)))
               (push (list tag-str closed heading marker) entries)))))))
    (if (null entries)
        (message "No %s tasks found" from)
      (setq entries (sort entries (lambda (a b)
                                    (string> (nth 1 a) (nth 1 b)))))
      (setq max-tag-width (apply #'max (mapcar (lambda (e) (length (nth 0 e))) entries)))
      (setq max-closed-width (apply #'max (mapcar (lambda (e) (length (nth 1 e))) entries)))
      (let* ((candidates
              (mapcar
               (lambda (entry)
                 (let* ((tag-str (nth 0 entry))
                        (closed (nth 1 entry))
                        (heading (nth 2 entry))
                        (marker (nth 3 entry))
                        (padded-tag (string-pad tag-str max-tag-width))
                        (padded-closed (string-pad closed max-closed-width))
                        (label (concat (propertize padded-tag 'face 'kwrooijen/faded-fg-face)
                                       "  "
                                       (propertize padded-closed 'face 'kwrooijen/faded-fg-face)
                                       "  " heading)))
                   (cons label marker)))
               entries))
             (collection (lambda (str pred action)
                          (if (eq action 'metadata)
                              '(metadata (display-sort-function . identity)
                                         (cycle-sort-function . identity))
                            (complete-with-action action (mapcar #'car candidates) str pred))))
             (selection (completing-read (format "%s -> %s: " from to) collection nil t))
             (marker (cdr (assoc selection candidates))))
        (when marker
          (with-current-buffer (marker-buffer marker)
            (goto-char marker)
            (org-todo to)
            (save-buffer)
            (kwrooijen/org-edit-subtree)))))))

(defvar kwrooijen/org-todo-find--candidates nil
  "Current candidates for `kwrooijen/org-todo-find-and-edit'.")

(defun kwrooijen/org-todo-find--kill-edit-indirect (marker)
  "Kill any edit-indirect buffer covering MARKER's position.
Signals an error if the edit-indirect buffer has unsaved changes."
  (with-current-buffer (marker-buffer marker)
    (when-let* ((ov (edit-indirect--search-for-edit-indirect
                     (marker-position marker) (1+ (marker-position marker))))
                (buf (overlay-get ov 'edit-indirect-buffer)))
      (when (buffer-live-p buf)
        (when (buffer-modified-p buf)
          (user-error "Edit-indirect buffer has unsaved changes — save or abort it first"))
        (with-current-buffer buf
          (edit-indirect-abort))))))

(defun kwrooijen/org-todo-find--throw-action (action)
  "Throw ACTION for the currently selected candidate."
  (when-let* ((cand (nth vertico--index vertico--candidates))
              (marker (cdr (assoc cand kwrooijen/org-todo-find--candidates))))
    (throw 'kwrooijen/org-todo-find--action (cons action marker))))

(defun kwrooijen/org-todo-find--change-state ()
  "Change the TODO state of the currently selected candidate."
  (interactive)
  (kwrooijen/org-todo-find--throw-action 'change-state))

(defun kwrooijen/org-todo-find--clock-in ()
  "Clock in the currently selected candidate."
  (interactive)
  (kwrooijen/org-todo-find--throw-action 'clock-in))

(defun kwrooijen/org-todo-find--clock-out ()
  "Clock out the currently selected candidate."
  (interactive)
  (kwrooijen/org-todo-find--throw-action 'clock-out))

(defun kwrooijen/org-todo-find--clock-pause ()
  "Pause the clock for the currently selected candidate."
  (interactive)
  (kwrooijen/org-todo-find--throw-action 'clock-pause))

(defun kwrooijen/org-todo-find--open-shell ()
  "Open the agent-shell for the currently selected candidate."
  (interactive)
  (kwrooijen/org-todo-find--throw-action 'open-shell))

(defun kwrooijen/org-todo-find--preview ()
  "Preview the currently selected candidate without closing the minibuffer."
  (interactive)
  (when-let* ((cand (nth vertico--index vertico--candidates))
              (marker (cdr (assoc cand kwrooijen/org-todo-find--candidates))))
    (let* ((buf (save-window-excursion
                  (with-current-buffer (marker-buffer marker)
                    (goto-char marker)
                    (kwrooijen/org-edit-subtree)
                    (goto-char (point-min))
                    (current-buffer))))
           (win (get-buffer-window buf)))
      (if (and win (window-live-p win))
          ;; Already visible — do nothing
          nil
        ;; Close existing popwin popup, then display the new buffer
        (popwin:close-popup-window)
        (popwin:display-buffer buf)))
    (select-window (active-minibuffer-window))))

(defvar kwrooijen/org-todo-find-map
  (let ((map (make-sparse-keymap)))
    (define-key map (kbd "C-t") #'kwrooijen/org-todo-find--change-state)
    (define-key map (kbd "C-i") #'kwrooijen/org-todo-find--clock-in)
    (define-key map (kbd "C-o") #'kwrooijen/org-todo-find--clock-out)
    (define-key map (kbd "C-p") #'kwrooijen/org-todo-find--clock-pause)
    (define-key map (kbd "C-s") #'kwrooijen/org-todo-find--open-shell)
    (define-key map (kbd "<tab>") #'kwrooijen/org-todo-find--preview)
    map)
  "Keymap active during `kwrooijen/org-todo-find-and-edit'.")

(cl-defun kwrooijen/org-todo-find-and-edit (&key states tag)
  "Select a task in one of STATES and open it with edit-subtree.
STATES is a list of todo state strings.
When TAG is non-nil, only show tasks that have that tag.
Press C-t to change the TODO state of the selected entry."
  (let ((keep-going t))
    (while keep-going
      (setq keep-going nil)
      (let (entries max-tag-width max-closed-width)
        (dolist (file (org-agenda-files))
          (with-current-buffer (find-file-noselect file)
            (org-with-wide-buffer
             (goto-char (point-min))
             (while (re-search-forward org-heading-regexp nil t)
               (when (and (member (org-get-todo-state) states)
                          (or (null tag)
                              (member tag (org-get-tags))))
                 (let* ((heading (org-get-heading t t t t))
                        (tags (org-get-tags))
                        (tag-str (if tags (string-join tags ":") ""))
                        (state (or (org-get-todo-state) ""))
                        (closed (or (org-entry-get nil "CLOSED") ""))
                        (id (org-entry-get nil "CLOCK_MULTI_ID"))
                        (clocked (and id (org-clock-multi--find-clock-by-key id) t))
                        (paused (and id (not clocked) (member id org-clock-multi-paused) t))
                        (marker (point-marker))
                        (project (org-entry-get nil "PROJECT" t))
                        (worktree (org-entry-get nil "WORKTREE"))
                        (agent-status
                         (if (and project worktree)
                             (let* ((wt-path (expand-file-name
                                              worktree
                                              (file-name-concat (expand-file-name project)
                                                                agent-shell-worktree--subdirectory)))
                                    (shell-buf (org-agent-shell--find-shell-buffer wt-path)))
                               (if shell-buf
                                   (agent-shell-manager--get-combined-status shell-buf)
                                 (propertize "No Shell" 'face 'font-lock-comment-face)))
                           "")))
                   (push (list tag-str state closed heading marker clocked paused agent-status) entries)))))))
        (if (null entries)
            (message "No %s tasks found" (string-join states "/"))
          (setq entries (sort entries (lambda (a b)
                                        (let ((ca (nth 5 a)) (cb (nth 5 b))
                                              (pa (nth 6 a)) (pb (nth 6 b)))
                                          (cond
                                           ((and ca (not cb)) t)
                                           ((and (not ca) cb) nil)
                                           ((and pa (not pb)) t)
                                           ((and (not pa) pb) nil)
                                           (t (string> (nth 2 a) (nth 2 b))))))))
          (setq max-tag-width (apply #'max (mapcar (lambda (e) (length (nth 0 e))) entries)))
          (let ((max-state-width (apply #'max (mapcar (lambda (e) (length (nth 1 e))) entries)))
                (max-agent-width (apply #'max (mapcar (lambda (e) (string-width (nth 7 e))) entries))))
            (setq max-closed-width (apply #'max (mapcar (lambda (e) (length (nth 2 e))) entries)))
            (let* ((candidates
                    (mapcar
                     (lambda (entry)
                       (let* ((tag-str (nth 0 entry))
                              (state (nth 1 entry))
                              (closed (nth 2 entry))
                              (heading (nth 3 entry))
                              (marker (nth 4 entry))
                              (clocked (nth 5 entry))
                              (paused (nth 6 entry))
                              (agent-status (nth 7 entry))
                              (padded-tag (string-pad tag-str max-tag-width))
                              (padded-agent (string-pad agent-status max-agent-width))
                              (padded-state (propertize (string-pad state max-state-width)
                                                        'face (org-get-todo-face state)))
                              (padded-closed (string-pad closed max-closed-width))
                              (label (concat (propertize padded-tag 'face 'kwrooijen/faded-fg-face)
                                             "  "
                                             padded-agent
                                             "  "
                                             padded-state
                                             "  "
                                             (propertize padded-closed 'face 'kwrooijen/faded-fg-face)
                                             "  " heading)))
                         (when clocked
                           (put-text-property 0 1 'kwrooijen/clocked t label))
                         (when paused
                           (put-text-property 0 1 'kwrooijen/paused t label))
                         (cons label marker)))
                     entries))
                   (kwrooijen/org-todo-find--candidates candidates)
                   (group-fn (lambda (cand transform)
                               (if transform cand
                                 (cond
                                  ((get-text-property 0 'kwrooijen/clocked cand) "Clocked In")
                                  ((get-text-property 0 'kwrooijen/paused cand) "Paused")
                                  (t "Tasks")))))
                   (collection (lambda (str pred action)
                                 (if (eq action 'metadata)
                                     `(metadata (display-sort-function . identity)
                                                (cycle-sort-function . identity)
                                                (group-function . ,group-fn))
                                   (complete-with-action action (mapcar #'car candidates) str pred))))
                   (result (catch 'kwrooijen/org-todo-find--action
                             (minibuffer-with-setup-hook
                                 (lambda () (use-local-map (make-composed-keymap
                                                            kwrooijen/org-todo-find-map
                                                            (current-local-map))))
                               (cons 'select (completing-read "Select: " collection nil t))))))
              (pcase (car result)
                ('select
                 (when-let* ((marker (cdr (assoc (cdr result) candidates))))
                   (with-current-buffer (marker-buffer marker)
                     (goto-char marker)
                     (kwrooijen/org-edit-subtree)
                     (goto-char (point-min)))))
                ('change-state
                 (let ((marker (cdr result))
                       (edit-indirect--inhibit-read-only t))
                   (kwrooijen/org-todo-find--kill-edit-indirect marker)
                   (with-current-buffer (marker-buffer marker)
                     (save-excursion
                       (goto-char marker)
                       (org-todo))))
                 (setq keep-going t))
                ('clock-in
                 (let ((marker (cdr result))
                       (edit-indirect--inhibit-read-only t))
                   (kwrooijen/org-todo-find--kill-edit-indirect marker)
                   (with-current-buffer (marker-buffer marker)
                     (save-excursion
                       (goto-char marker)
                       (org-clock-multi-clock-in))))
                 (setq keep-going t))
                ('clock-out
                 (let ((marker (cdr result))
                       (edit-indirect--inhibit-read-only t))
                   (kwrooijen/org-todo-find--kill-edit-indirect marker)
                   (with-current-buffer (marker-buffer marker)
                     (save-excursion
                       (goto-char marker)
                       (org-clock-multi-clock-out))))
                 (setq keep-going t))
                ('clock-pause
                 (let ((marker (cdr result))
                       (edit-indirect--inhibit-read-only t))
                   (kwrooijen/org-todo-find--kill-edit-indirect marker)
                   (with-current-buffer (marker-buffer marker)
                     (save-excursion
                       (goto-char marker)
                       (org-clock-multi-clock-pause))))
                 (setq keep-going t))
                ('open-shell
                 (let ((marker (cdr result)))
                   (with-current-buffer (marker-buffer marker)
                     (save-excursion
                       (goto-char marker)
                       (org-agent-shell-open-shell)))))))))))))

(defun kwrooijen/org-todo-find-done-work ()
  "Select a DONE or CANCELLED :work: task and edit it."
  (interactive)
  (kwrooijen/org-todo-find-and-edit :states '("DONE" "CANCELLED") :tag "work"))

(defun kwrooijen/org-todo-find-waiting-work ()
  "Select a WAITING :work: task and edit it."
  (interactive)
  (kwrooijen/org-todo-find-and-edit :states '("WAITING") :tag "work"))

(defun kwrooijen/org-todo-find-active-work ()
  "Select a TODO or IN PROGRESS :work: task and edit it."
  (interactive)
  (kwrooijen/org-todo-find-and-edit :states '("TODO" "IN PROGRESS" "WAITING") :tag "work"))

(defun kwrooijen/org-todo-unarchive ()
  "Select a DONE task and change it back to TODO."
  (interactive)
  (kwrooijen/org-todo-select-and-change :from "DONE" :to "TODO"))

(defun kwrooijen/org-todo-unarchive-work ()
  "Select a DONE :work: task and change it back to TODO."
  (interactive)
  (kwrooijen/org-todo-select-and-change :from "DONE" :to "TODO" :tag "work"))

(defun kwrooijen/org-todo-unwait ()
  "Select a WAITING task and change it back to TODO."
  (interactive)
  (kwrooijen/org-todo-select-and-change :from "WAITING" :to "TODO"))

(defun kwrooijen/org-todo-unwait-work ()
  "Select a WAITING :work: task and change it back to TODO."
  (interactive)
  (kwrooijen/org-todo-select-and-change :from "WAITING" :to "TODO" :tag "work"))
#+END_SRC



Custom loading bar for work.
#+BEGIN_SRC emacs-lisp


(defun kwrooijen/org-agenda-loading-bar ()
  "Return a textual loading bar based on today's clocked minutes."
  (let* ((max 480.0)
         (current (float (kwrooijen/org-clock-clocked-today)))
         (ratio (min 1.0 (/ current max)))
         (width 30)
         (filled (round (* width ratio)))
         (empty (- width filled))
         (percent (round (* 100 ratio))))
    (format
     "%s\n\nWork Progress: [%s%s] %d%% (%d / 480 min)\n\n%s\n\n"
     (make-string (- (window-width) 1) ?─)
     (make-string filled ?█)
     (make-string empty ?░)
     percent
     (round current)
     (make-string (- (window-width) 1) ?─))))

(defun kwrooijen/org-agenda-insert-loading-bar ()
  "Insert loading bar at the top of the agenda buffer."
  ;; TODO only when current day is mon - fri
  ;; (when (derived-mode-p 'org-agenda-mode)
  ;;   (let ((inhibit-read-only t))
  ;;     (goto-char (point-min))
  ;;     (insert (kwrooijen/org-agenda-loading-bar))))
)

(add-hook 'org-agenda-finalize-hook
          #'kwrooijen/org-agenda-insert-loading-bar)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/org-client-names ()
  "Return a list of client names derived from org files."
  (let ((dir (expand-file-name "~/Documents/org/todos/client/")))
    (mapcar #'file-name-base
            (directory-files dir nil "\\.org$"))))

(defun kwrooijen/org-set-clockreport-match (client)
  "Update only :match in `org-agenda-clockreport-parameter-plist`."
  (interactive
   (list (completing-read "Clockreport match: " (append (kwrooijen/org-client-names) '("work")) nil nil)))
  (setq org-agenda-clockreport-parameter-plist
        (plist-put org-agenda-clockreport-parameter-plist
                   :match
                   client))
  (org-agenda-redo))
#+END_SRC


#+BEGIN_SRC emacs-lisp
(evil-set-initial-state 'org-agenda-mode 'normal)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/org-agenda-edit-subtree ()
  "Edit the subtree at point in an indirect buffer.
Use C-c C-c to commit changes, C-c C-k to abort."
  (interactive)
  (let* ((marker (or (org-get-at-bol 'org-marker)
                     (org-agenda-error)))
         (buffer (marker-buffer marker))
         (pos (marker-position marker)))
    (with-current-buffer buffer
      (goto-char pos)
      (kwrooijen/org-edit-subtree))))

(evil-define-key 'normal org-agenda-mode-map (kbd "<tab>") #'kwrooijen/org-agenda-edit-subtree)
#+END_SRC

Add functionality to toggle habits in agenda.

#+BEGIN_SRC emacs-lisp
(defvar kwrooijen/org-habit-shown-p t)

(defun kwrooijen/org-habit-toggle ()
  (interactive)
  (setq kwrooijen/org-habit-shown-p (not kwrooijen/org-habit-shown-p))
  (org-agenda-redo))

(require 'org-habit)
#+END_SRC

** Custom :custom:

Adjsut the org agenda format. We add `EFFORT` (`[%e]`) to the `agenda`
view. (NOTE: Removed `%e` for now, to see how we can organize it nicely

#+BEGIN_SRC emacs-lisp
(setq org-agenda-prefix-format
 '((agenda . "[%-5e] %-12:c%?-12t% s")
   (todo . " %i %-12:c")
   (tags . " %i %-12:c")
   (search . " %i %-12:c")))
#+END_SRC

Don't show scheduled TODOs if they have been done today

#+BEGIN_SRC emacs-lisp
(org-agenda-todo-ignore-scheduled 'future)
#+END_SRC

Org agenda table, include ASANA in header for ticket ids

#+BEGIN_SRC emacs-lisp
(org-agenda-clockreport-parameter-plist
 (list :maxlevel 2
       :narrow nil
       :properties '("ASANA")
       :match "+work"
       :indent nil
       :tcolumns 1))
#+END_SRC

List of keywords which a TODO heading can have.

#+BEGIN_SRC emacs-lisp
(org-todo-keywords '((sequence
                      "TODO(t)"
                      "IN PROGRESS(p)"
                      "WAITING(w)"
                      "|"
                      "CANCELLED(c!)"
                      "DONE(d!)")))
#+END_SRC

Set the faces for specific org keywords

#+BEGIN_SRC emacs-lisp
(org-todo-keyword-faces
      '(("TODO"        . (:inherit kwrooijen/salient-fg-face))
        ("IN PROGRESS" . (:inherit kwrooijen/popout-fg-face))
        ("WAITING"     . (:inherit kwrooijen/faded-fg-face))
        ("DONE"        . (:inherit org-done))
        ("CANCELLED"   . (:inherit org-done))))
#+END_SRC


Customize agenda renderer to optionally filter out habits.

#+BEGIN_SRC emacs-lisp
(org-agenda-skip-function-global
 (lambda ()
   (when (and (org-is-habit-p) (not kwrooijen/org-habit-shown-p))
     (org-agenda-skip-entry-if 'todo 'any))))
#+END_SRC



** General :general:
#+BEGIN_SRC emacs-lisp
(kwrooijen/leader
  "aa" '(org-agenda-list :which-key "Org Agenda")
  "ao" '(org-agenda :which-key "Org Commands")
  "at" '(org-todo-list :which-key "Org Todos")
  "ad" '(kwrooijen/org-agenda-done :which-key "Org Done")
  "ac" '(org-capture :which-key "Org Capture")
  "aD" '(org-todo-dashboard-open :which-key "Dashboard")
  "af" '(kwrooijen/org-todo-find-active-work :which-key "Find Active")
  "au" '(kwrooijen/org-todo-find-done-work :which-key "Find Done")
  "aw" '(kwrooijen/org-todo-find-waiting-work :which-key "Find Waiting"))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(general-define-key
:states '(normal)
:keymaps 'org-agenda-mode-map
  (kbd "TAB") #'kwrooijen/org-agenda-edit-subtree
  (kbd "RET") #'org-agenda-switch-to
  (kbd "SPC") #'org-agenda-show-and-scroll-up

  (kbd "C-k") #'org-agenda-kill
  (kbd "C-n") #'org-agenda-next-line
  (kbd "C-p") #'org-agenda-previous-line
  ;; "C-_" #'org-agenda-undo
  "!" #'org-agenda-toggle-deadlines
  "#" #'org-agenda-dim-blocked-tasks
  "$" #'org-agenda-archive
  "%" #'org-agenda-bulk-mark-regexp
  "+" #'org-agenda-priority-up
  "," #'org-agenda-priority
  "-" #'org-agenda-priority-down
  "." #'org-agenda-goto-today
  "/" #'org-agenda-filter
  ":" #'org-agenda-set-tags
  ";" #'kwrooijen/org-set-clockreport-match
  "<" #'org-agenda-filter-by-category
  "=" #'org-agenda-filter-by-regexp
   ">" #'org-agenda-date-prompt
  "?" #'org-agenda-show-the-flagging-note
  "A" #'org-agenda-append-agenda
  "B" #'org-agenda-bulk-action
  "C" #'org-agenda-convert-date
  "D" #'org-agenda-toggle-diary
  "E" #'org-agenda-entry-text-mode
  "F" #'org-agenda-follow-mode
  "G" #'org-agenda-toggle-time-grid
  "H" #'org-agenda-holidays
  "I" #'org-clock-multi-agenda-clock-in
  "K" #'org-habit-toggle-display-in-agenda
  "L" #'org-agenda-recenter
  "N" #'org-agenda-next-item
  "O" #'org-clock-multi-agenda-clock-out
  "P" #'org-clock-multi-agenda-clock-pause
  "Q" #'org-agenda-Quit
  "R" #'org-agenda-clockreport-mode
  "V" #'org-clock-data-agenda-chart-mode
  "S" #'org-agenda-sunrise-sunset
  "T" #'org-agenda-show-tags
  (kbd "M-m") #'org-agenda-bulk-unmark
  (kbd "M-M") #'org-agenda-bulk-unmark-all
  (kbd "C-o") #'org-clock-multi-agenda-clock-out-all
  "m" #'org-agenda-bulk-mark
  "M" #'org-agenda-bulk-mark-all
  "X" #'org-clock-multi-agenda-clock-cancel
  "[" #'org-agenda-manipulate-query-add
  "\\" #'org-agenda-filter-by-tag
  "]" #'org-agenda-manipulate-query-subtract
  "^" #'org-agenda-filter-by-top-headline
  "_" #'org-agenda-filter-by-effort
  "a" #'org-agenda-archive-default-with-confirmation
  "b" #'org-agenda-earlier
  "c" #'org-agenda-goto-calendar
  "d" #'org-agenda-day-view
  "e" #'org-agenda-set-effort
  "f" #'org-agenda-later
  "g" #'org-agenda-redo-all
  "H" #'org-agenda-holidays
  "i" #'org-agenda-diary-entry
  "n" #'org-agenda-goto-date
  "p" #'org-agenda-capture
  "L" #'org-agenda-log-mode
  "o" #'org-agenda-month-view

  "k" #'org-agenda-previous-line
  "j" #'org-agenda-next-line
  "l" #'evil-forward-char
  "h" #'evil-backward-char

  "q" #'org-agenda-quit
  "r" #'org-agenda-redo
  "s" #'org-save-all-org-buffers
  "t" #'org-agenda-todo
  "u" #'org-agenda-undo
  "U" #'org-agenda-redo
  "v" #'org-agenda-view-mode-dispatch
  "w" #'org-agenda-week-view
  "x" #'org-agenda-exit
  "y" #'org-agenda-year-view
  "z" #'org-agenda-add-note
  "{" #'org-agenda-manipulate-query-add-re
  "|" #'org-agenda-filter-remove-all
  "}" #'org-agenda-manipulate-query-subtract-re
  "~" #'org-agenda-limit-interactively
  (kbd "DEL") #'org-agenda-show-scroll-down)
#+END_SRC


* Org Capture
:PROPERTIES:
:PACKAGE:  org-capture
:END:

** Init :init:

Read org capture templates from other file.

#+BEGIN_SRC emacs-lisp
(kwrooijen/find-and-load-file "~/.dotfiles/org-capture-templates.el")
#+END_SRC


* Calendar
:PROPERTIES:
:PACKAGE:  calendar
:END:

** Config :config:

#+BEGIN_SRC emacs-lisp
(evil-set-initial-state 'calendar-mode 'normal)
#+END_SRC

** General :general:
#+BEGIN_SRC emacs-lisp
(general-define-key
 :states '(normal)
 :keymaps 'calendar-mode-map
 (kbd "RET") #'org-calendar-select
 "C-@" #'calendar-set-mark
 "C-a" #'calendar-beginning-of-week
 "h" #'calendar-backward-day
 "l" #'calendar-forward-day
 "j" #'calendar-forward-week
 "k" #'calendar-backward-week
 "C-e" #'calendar-end-of-week
 "C-l" #'calendar-recenter
 "C-v" #'calendar-scroll-left-three-months
 (kbd "SPC") #'scroll-other-window
 "-" #'negative-argument
 "." #'calendar-goto-today
 "<" #'calendar-scroll-right
 ">" #'calendar-scroll-left
 "?" #'calendar-goto-info-node
 "D" #'diary-view-other-diary-entries
 "M" #'calendar-lunar-phases
 "S" #'calendar-sunrise-sunset
 "[" #'calendar-backward-year
 "]" #'calendar-forward-year
 "a" #'calendar-list-holidays
 "c" #'org-calendar-goto-agenda
 "d" #'diary-view-entries
 ;; "h" #'calendar-cursor-holidays
 "m" #'diary-mark-entries
 "o" #'calendar-other-month
 "q" #'calendar-exit
 "s" #'diary-show-all-entries
 "u" #'calendar-unmark
 "x" #'calendar-mark-holidays
 "{" #'calendar-backward-month
 "}" #'calendar-forward-month
 ;; "DEL" #'scroll-other-window-down
 ;; "S-SPC" #'scroll-other-window-down
 ;; "C-SPC" #'calendar-set-mark

 (kbd "C-c C-l") #'calendar-redraw

 (kbd "C-x C-x") #'calendar-exchange-point-and-mark
 (kbd "C-x <") #'calendar-scroll-right
 (kbd "C-x >") #'calendar-scroll-left
 (kbd "C-x [") #'calendar-backward-year
 (kbd "C-x ]") #'calendar-forward-year

 (kbd "M-<") #'calendar-beginning-of-year
 (kbd "M-=") #'calendar-count-days-region
 (kbd "M->") #'calendar-end-of-year
 (kbd "M-a") #'calendar-beginning-of-month
 (kbd "M-e") #'calendar-end-of-month
 (kbd "M-v") #'calendar-scroll-right-three-months
 (kbd "M-{") #'calendar-backward-month
 (kbd "M-}") #'calendar-forward-month

 "A a" #'appt-add
 "A d" #'appt-delete

 "H m" #'cal-html-cursor-month
 "H y" #'cal-html-cursor-year

 "g C" #'calendar-chinese-goto-date
 "g D" #'calendar-goto-day-of-year
 "g a" #'calendar-astro-goto-day-number
 "g b" #'calendar-bahai-goto-date
 "g c" #'calendar-iso-goto-date
 "g d" #'calendar-goto-date
 "g e" #'calendar-ethiopic-goto-date
 "g f" #'calendar-french-goto-date
 "g h" #'calendar-hebrew-goto-date
 "g i" #'calendar-islamic-goto-date
 "g j" #'calendar-julian-goto-date
 "g k" #'calendar-coptic-goto-date
 "g p" #'calendar-persian-goto-date
 "g w" #'calendar-iso-goto-week

 "i a" #'diary-insert-anniversary-entry
 "i b" #'diary-insert-block-entry
 "i c" #'diary-insert-cyclic-entry
 "i d" #'diary-insert-entry
 "i m" #'diary-insert-monthly-entry
 "i w" #'diary-insert-weekly-entry
 "i y" #'diary-insert-yearly-entry

 "p C" #'calendar-chinese-print-date
 "p a" #'calendar-astro-print-day-number
 "p b" #'calendar-bahai-print-date
 "p c" #'calendar-iso-print-date
 "p d" #'calendar-print-day-of-year
 "p e" #'calendar-ethiopic-print-date
 "p f" #'calendar-french-print-date
 "p h" #'calendar-hebrew-print-date
 "p i" #'calendar-islamic-print-date
 "p j" #'calendar-julian-print-date
 "p k" #'calendar-coptic-print-date
 "p m" #'calendar-mayan-print-date
 "p o" #'calendar-print-other-dates
 "p p" #'calendar-persian-print-date

 "t M" #'cal-tex-cursor-month-landscape
 "t Y" #'cal-tex-cursor-year-landscape
 "t d" #'cal-tex-cursor-day
 "t m" #'cal-tex-cursor-month
 "t y" #'cal-tex-cursor-year

 "t f W" #'cal-tex-cursor-filofax-week
 "t f d" #'cal-tex-cursor-filofax-daily
 "t f w" #'cal-tex-cursor-filofax-2week
 "t f y" #'cal-tex-cursor-filofax-year

 "t w 1" #'cal-tex-cursor-week
 "t w 2" #'cal-tex-cursor-week2
 "t w 3" #'cal-tex-cursor-week-iso
 "t w 4" #'cal-tex-cursor-week-monday
 "t w W" #'cal-tex-cursor-week2-summary)
#+END_SRC


* Org Clock
:PROPERTIES:
:PACKAGE:  org-clock
:END:
** Config :config:
#+BEGIN_SRC emacs-lisp
(defun kwrooijen/org-clock--today-range ()
  "Return (START . END) of today in Unix seconds."
  (let* ((now (decode-time (current-time)))
         (start (float-time
                 (encode-time 0 0 0
                              (nth 3 now)
                              (nth 4 now)
                              (nth 5 now)))))
    (cons start (+ start 86400))))


(defun kwrooijen/org-clock--today-start ()
  "Return Unix time (seconds) for start of today."
  (let* ((now (decode-time (current-time))))
    (encode-time 0 0 0
                 (nth 3 now)   ; day
                 (nth 4 now)   ; month
                 (nth 5 now)))) ; year

(defun kwrooijen/org-clock--open-clocked-today ()
  "Return minutes from currently running clocks that fall within today.
Uses org-clock-multi-clocks for multiple simultaneous clocks.
Note: Clock start times are stored in minutes since epoch."
  (let ((total 0))
    ;; Convert day boundaries to minutes for consistent comparison
    (pcase-let* ((`(,day-start-sec . ,day-end-sec) (kwrooijen/org-clock--today-range))
                 (day-start (floor (/ day-start-sec 60)))
                 (day-end (floor (/ day-end-sec 60)))
                 (now (floor (/ (float-time (current-time)) 60))))
      (dolist (clock org-clock-multi-clocks)
        (let ((clock-start (cdr clock)))  ; already in minutes
          (when (< clock-start day-end)
            (setq total (+ total
                           (max 0 (- (min now day-end)
                                     (max clock-start day-start)))))))))
    (when (> total 0) total)))

(defun kwrooijen/org-clock-clocked-today (&optional as-duration)
  "Return total time clocked today across `org-agenda-files`.
If AS-DURATION is non-nil, return an Org duration string."
  (let* ((start (float-time (kwrooijen/org-clock--today-start)))
         (end   (+ start 86400))
         (minutes (or (kwrooijen/org-clock--open-clocked-today) 0)))
    (dolist (file (org-agenda-files))
      (with-current-buffer (find-file-noselect file)
        (setq minutes
              (+ minutes (org-clock-sum start end)))))
    (if as-duration
        (org-duration-from-minutes minutes)
      minutes)))
#+END_SRC

* Org Clock Data
:PROPERTIES:
:PACKAGE:  org-clock-data
:END:

** Custom :custom:
#+BEGIN_SRC emacs-lisp
(org-clock-data-bar-value 'earnings)
#+END_SRC
