#+PRIORITY: 3
* Functions

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/find-and-load-file (file)
  (let ((file-path (expand-file-name file)))
    (when (file-exists-p file-path)
      (load-file file-path))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/upgrade-packages ()
  "Upgrades all packages to their latest version (using Straight.el) and updates the lockfile."
  (interactive)
  (straight-pull-all)
  ;; KEY CHORD WORKING VERSION
  ;; ("key-chord" . "fc75b1451759121601110da8ddfadcf5156318af")
  (straight-freeze-versions))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/capitalize-previous-word ()
  (interactive)
  (save-excursion
    (backward-word)
    (capitalize-word 1)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/tab ()
  (interactive)
  (or (copilot-accept-completion)
      (indent-for-tab-command)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/toggle-word-line-wrap ()
  (interactive)
  (toggle-truncate-lines))
#+END_SRC


#+BEGIN_SRC emacs-lisp
(defun kwrooijen/org-latex-preview-all ()
  "Preview all LaTeX fragments in the current buffer."
  (interactive)
  (org-latex-preview '(16)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/org-images-increase ()
  (interactive)
  (when (equal org-image-actual-width t)
    (setq org-image-actual-width 1200))
  (setq org-image-actual-width (+ (or org-image-actual-width 1000) 250))
  (kwrooijen/org-toggle-inline-images-refresh))

(defun kwrooijen/org-images-decrease ()
  (interactive)
  (when (equal org-image-actual-width t)
    (setq org-image-actual-width 1200))
  (setq org-image-actual-width (- (or org-image-actual-width 1000) 250))
  (kwrooijen/org-toggle-inline-images-refresh))

(defun kwrooijen/org-toggle-inline-images-refresh ()
  "Force refresh of inline images in the current buffer."
  (interactive)
  (org-remove-inline-images)
  (org-display-inline-images))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/org-focus-this-heading ()
  "Close all other Org headings except the current one."
  (interactive)
  (org-overview)
  (org-reveal)
  (org-show-subtree))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/indent-buffer ()
  (interactive)
  (indent-region (point-min) (point-max)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/load-secret-env ()
  "Load environment variables from FILE-PATH into Emacs."
  (interactive)
  (let ((files '("~/.env.gpg" "~/.env-emacs.gpg")))
    (dolist (file files)
      (when (file-exists-p file)
        (with-temp-buffer
          (insert-file-contents file)
          (goto-char (point-min))
          (while (not (eobp))
            (let ((line (buffer-substring-no-properties
                         (line-beginning-position)
                         (line-end-position))))
              (when (string-match "^export \\([^=]+\\)=\\(.*\\)$" line)
                (setenv (match-string 1 line)
                        (match-string 2 line))))
            (forward-line 1)))))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/read-file (file)
  (with-temp-buffer
    (insert-file-contents (expand-file-name file))
    (buffer-string)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/project-root ()
  (project-root (project-current)))

(defun kwrooijen/project-name ()
  (project-name (project-current)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/make--targets (makefile)
  "Parse MAKEFILE and return a list of target names."
  (when (file-exists-p makefile)
    (with-temp-buffer
      (insert-file-contents makefile)
      (let (targets)
        (goto-char (point-min))
        (while (re-search-forward "^\\([a-zA-Z0-9_-][a-zA-Z0-9._-]*\\):" nil t)
          (push (match-string 1) targets))
        (nreverse targets)))))

(defun kwrooijen/make ()
  "Select a Makefile target via completing-read and run it in a unique buffer."
  (interactive)
  (let* ((default-directory (or (kwrooijen/project-root) default-directory))
         (makefile (expand-file-name "Makefile" default-directory))
         (targets (kwrooijen/make--targets makefile))
         (target (completing-read "Make: " targets nil t))
         (buf-name (format "make:%s:%s" (kwrooijen/project-name) target)))
    (pop-to-buffer (make-comint-in-buffer buf-name nil "make" nil target))))

(defmacro comment (&rest _))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/dired-project-root ()
  (interactive)
  (let ((project-root (project-root (project-current t))))
    (dired project-root)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/remove-empty-lines ()
  "Remove all empty lines in the current buffer or region."
  (interactive)
  (let ((start (if (use-region-p) (region-beginning) (point-min)))
        (end (if (use-region-p) (region-end) (point-max))))
    (save-excursion
      (goto-char start)
      (while (re-search-forward "^[[:space:]]*\n" end t)
        (replace-match "")))))
#+END_SRC
