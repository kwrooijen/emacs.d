#+LEXICAL_BINDING: nil
#+PRIORITY: 2

* Nord Theme
:PROPERTIES:
:PACKAGE: nord-theme
:STRAIGHT: (:host github :repo "nordtheme/emacs")
:END:

A modular theming framework built on the Nord color palette.

** Color Reference

Use =M-x kwrooijen/hex-colors-enable= to preview colors in this buffer.

| Semantic    | Hex     | Description                          |
|-------------+---------+--------------------------------------|
| strong      | #FFFFFF | Primary emphasis, bold text          |
| foreground  | #ECEFF4 | Default text                         |
| subtle      | #434C5E | Secondary, muted elements            |
| faded       | #616E88 | De-emphasized, comments              |
| salient     | #81A1C1 | Accent, links, keywords              |
| popout      | #D08770 | Strings, special callouts            |
| error       | #BF616A | Error states                         |
| warning     | #FFC16D | Warning states                       |
| success     | #A3BE8C | Success states                       |
| info        | #88C0D0 | Informational highlights             |
| background  | #222436 | Main background                      |
| darken      | #1e2030 | Darker background (sidebars, blocks) |
| highlight   | #2f334d | Selection, cursor line               |

** Init :init:

*** Palette Definition

#+BEGIN_SRC emacs-lisp
(require 'color)

(defgroup kwrooijen/theme nil
  "Kwrooijen theme configuration."
  :group 'faces)

(defvar kwrooijen/palette-dark
  '(:strong     "#FFFFFF"
    :foreground "#ECEFF4"
    :subtle     "#434C5E"
    :faded      "#616E88"
    :salient    "#81A1C1"
    :popout     "#D08770"
    :error      "#BF616A"
    :warning    "#FFC16D"
    :success    "#A3BE8C"
    :info       "#88C0D0"
    :background "#222436"
    :darken     "#1e2030"
    :highlight  "#2f334d")
  "Dark theme palette.")

(defvar kwrooijen/palette-light
  '(:strong     "#000000"
    :foreground "#101010"
    :subtle     "#9099AB"
    :faded      "#7A8599"
    :salient    "#3B6EA8"
    :popout     "#C35D2A"
    :error      "#C33C54"
    :warning    "#AC8300"
    :success    "#6A9A3A"
    :info       "#2C9A9A"
    :background "#FAFAFA"
    :darken     "#F0F0F0"
    :highlight  "#E5E5E5")
  "Light theme palette (basic, can be improved).")

(defvar kwrooijen/palette kwrooijen/palette-dark
  "Active color palette.")

(defvar kwrooijen/theme-variant 'nord
  "Base theme to load (nord).")

(defvar kwrooijen/font "Roboto Mono"
  "Default font family.")
#+END_SRC

*** Color Access API

#+BEGIN_SRC emacs-lisp
(defun kwrooijen-color (name)
  "Get color NAME from the active palette.
NAME should be a keyword like :salient, :error, etc."
  (plist-get kwrooijen/palette name))

(defun kwrooijen-colors ()
  "Return list of all color names in the palette."
  (cl-loop for (key _val) on kwrooijen/palette by #'cddr
           collect key))
#+END_SRC

*** Color Transformation Functions

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/color-increase-brightness (hex &optional percent)
  "Adjust brightness of HEX by PERCENT (default 10).
Positive values brighten, negative values darken.
Returns a #RRGGBB string."
  (let* ((p (/ (or percent 10) 100.0))
         (rgb (color-name-to-rgb hex))
         (adjusted
          (mapcar
           (lambda (c)
             (cond
              ((> p 0) (min 1.0 (+ c (* (- 1.0 c) p))))
              ((< p 0) (max 0.0 (* c (+ 1.0 p))))
              (t c)))
           rgb)))
    (apply #'color-rgb-to-hex (append adjusted (list 2)))))

(defun kwrooijen/color-increase-saturation (hex &optional percent)
  "Adjust saturation of HEX by PERCENT (default 10).
Returns a #RRGGBB string."
  (let* ((p (/ (or percent 10) 100.0))
         (rgb (color-name-to-rgb hex))
         (hsl (apply #'color-rgb-to-hsl rgb))
         (h (nth 0 hsl))
         (s (nth 1 hsl))
         (l (nth 2 hsl))
         (new-s (min 1.0 (+ s (* (- 1.0 s) p))))
         (new-rgb (color-hsl-to-rgb h new-s l)))
    (apply #'color-rgb-to-hex (append new-rgb (list 2)))))
#+END_SRC

*** Face Generation System

#+BEGIN_SRC emacs-lisp
(defvar kwrooijen/face-registry '()
  "Registry of all generated theme faces.")

(defun kwrooijen/define-face (name color &optional bg-color)
  "Define faces for semantic NAME with COLOR.
Creates three faces:
  - kwrooijen/NAME-fg-face (foreground only)
  - kwrooijen/NAME-bg-face (background only)
  - kwrooijen/NAME-fb-face (foreground + background)
Optional BG-COLOR sets a different background for fb-face."
  (let ((fg-face (intern (format "kwrooijen/%s-fg-face" name)))
        (bg-face (intern (format "kwrooijen/%s-bg-face" name)))
        (fb-face (intern (format "kwrooijen/%s-fb-face" name))))
    ;; Define faces if they don't exist
    (unless (facep fg-face)
      (eval `(defface ,fg-face '((t (:foreground ,color))) "")))
    (unless (facep bg-face)
      (eval `(defface ,bg-face '((t (:background ,color))) "")))
    (unless (facep fb-face)
      (eval `(defface ,fb-face '((t (:foreground ,color :background ,(or bg-color color)))) "")))
    ;; Update face attributes
    (set-face-attribute fg-face nil :foreground color)
    (set-face-attribute bg-face nil :background color)
    (set-face-attribute fb-face nil
                        :foreground color
                        :background (or bg-color color))
    ;; Register faces
    (cl-pushnew fg-face kwrooijen/face-registry)
    (cl-pushnew bg-face kwrooijen/face-registry)
    (cl-pushnew fb-face kwrooijen/face-registry)))

(defun kwrooijen/reset-face-attributes (&rest faces)
  "Reset all attributes of FACES to unspecified."
  (dolist (face faces)
    (set-face-attribute
     face nil
     :foreground 'unspecified
     :background 'unspecified
     :inherit 'unspecified
     :stipple 'unspecified
     :inverse-video 'unspecified
     :box 'unspecified
     :strike-through 'unspecified
     :overline 'unspecified
     :underline 'unspecified
     :slant 'unspecified
     :weight 'unspecified
     :height 'unspecified
     :width 'unspecified
     :foundry 'unspecified
     :family 'unspecified)))

(defun kwrooijen/reset-inherit-face! (&rest face-pairs)
  "Reset FACE-PAIRS and set inheritance.
FACE-PAIRS is a list of (face inherit-face) pairs."
  (dolist (face-pair (-partition 2 face-pairs))
    (let ((face (car face-pair))
          (inherit-face (cadr face-pair)))
      (kwrooijen/reset-face-attributes face)
      (set-face-attribute face nil :inherit inherit-face))))
#+END_SRC

*** Theme Application

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/apply-palette (palette)
  "Apply PALETTE to generate all base faces."
  (setq kwrooijen/palette palette)
  ;; Generate base semantic faces
  (kwrooijen/define-face 'background  (plist-get palette :background))
  (kwrooijen/define-face 'foreground  (plist-get palette :foreground))
  (kwrooijen/define-face 'strong      (plist-get palette :strong))
  (kwrooijen/define-face 'faded       (plist-get palette :faded))
  (kwrooijen/define-face 'subtle      (plist-get palette :subtle))
  (kwrooijen/define-face 'salient     (plist-get palette :salient))
  (kwrooijen/define-face 'popout      (plist-get palette :popout))
  (kwrooijen/define-face 'error       (plist-get palette :error))
  (kwrooijen/define-face 'warning     (plist-get palette :warning))
  (kwrooijen/define-face 'success     (plist-get palette :success))
  (kwrooijen/define-face 'info        (plist-get palette :info))
  (kwrooijen/define-face 'darken      (plist-get palette :darken))
  (kwrooijen/define-face 'highlight   (plist-get palette :highlight))
  ;; Composite faces (foreground + different background)
  (kwrooijen/define-face 'faded-darken
                         (plist-get palette :faded)
                         (plist-get palette :darken))
  ;; Update theme default face
  (custom-theme-set-faces
   kwrooijen/theme-variant
   `(default ((t (:foreground ,(plist-get palette :foreground)
                  :background ,(plist-get palette :background)))))))

(defun kwrooijen/apply-core-faces ()
  "Apply theme faces to core Emacs faces."
  ;; Bold
  (kwrooijen/reset-inherit-face! 'bold 'kwrooijen/strong-fg-face)
  (set-face-attribute 'bold nil :weight 'bold)
  ;; Basic UI faces
  (kwrooijen/reset-inherit-face!
   'secondary-selection 'kwrooijen/darken-bg-face
   'region              'kwrooijen/highlight-bg-face
   'highlight           'kwrooijen/highlight-bg-face
   'success             'kwrooijen/success-fg-face
   'error               'kwrooijen/error-fg-face
   'warning             'kwrooijen/warning-fg-face
   'button              'kwrooijen/popout-fg-face
   ;; 'fringe              'kwrooijen/background-bg-face
   'minibuffer-prompt   'kwrooijen/salient-fg-face
   'header-line         'kwrooijen/foreground-fg-face)
  ;; Search faces
  (kwrooijen/reset-inherit-face!
   'isearch        'kwrooijen/faded-bg-face
   'lazy-highlight 'kwrooijen/subtle-bg-face
   'match          'bold)
  ;; Font-lock faces
  (kwrooijen/reset-inherit-face!
   'font-lock-comment-face       'kwrooijen/faded-fg-face
   'font-lock-doc-face           'kwrooijen/faded-fg-face
   'font-lock-string-face        'kwrooijen/popout-fg-face
   'font-lock-constant-face      'kwrooijen/salient-fg-face
   'font-lock-warning-face       'kwrooijen/popout-fg-face
   'font-lock-function-name-face 'kwrooijen/strong-fg-face
   'font-lock-variable-name-face 'kwrooijen/strong-fg-face
   'font-lock-builtin-face       'kwrooijen/salient-fg-face
   'font-lock-type-face          'kwrooijen/salient-fg-face
   'font-lock-keyword-face       'kwrooijen/salient-fg-face)
  ;; Dired
  (kwrooijen/reset-inherit-face! 'dired-directory 'kwrooijen/salient-fg-face))

(defun kwrooijen/set-dark-theme ()
  "Activate dark theme."
  (interactive)
  (kwrooijen/apply-palette kwrooijen/palette-dark)
  (kwrooijen/apply-core-faces)
  (set-frame-parameter nil 'background-color (kwrooijen-color :background)))

(defun kwrooijen/set-light-theme ()
  "Activate light theme."
  (interactive)
  (kwrooijen/apply-palette kwrooijen/palette-light)
  (kwrooijen/apply-core-faces)
  (set-frame-parameter nil 'background-color (kwrooijen-color :background)))

(defun kwrooijen/toggle-theme ()
  "Toggle between dark and light themes."
  (interactive)
  (if (equal kwrooijen/palette kwrooijen/palette-dark)
      (kwrooijen/set-light-theme)
    (kwrooijen/set-dark-theme)))
#+END_SRC

*** Initialize Theme

#+BEGIN_SRC emacs-lisp
;; Load base nord theme
(let ((inhibit-message t)
      (message-log-max nil))
  (load-theme kwrooijen/theme-variant t))

;; Set font
(set-face-attribute 'default nil
                    :family kwrooijen/font
                    :weight 'thin)

;; Apply dark theme
(kwrooijen/set-dark-theme)
#+END_SRC

* Font Size
:PROPERTIES:
:PACKAGE: frame
:END:

** Init :init:

#+BEGIN_SRC emacs-lisp
(defcustom kwrooijen/default-font-size 200
  "Default font size (in 1/10 pt)."
  :type 'integer
  :group 'kwrooijen/theme)

(defcustom kwrooijen/font-step-size 20
  "Font size adjustment step."
  :type 'integer
  :group 'kwrooijen/theme)

(defun kwrooijen/font-size-set (size)
  "Set global font SIZE."
  (set-face-attribute 'default nil :height size))

(defun kwrooijen/font-size-increase ()
  "Increase global font size."
  (interactive)
  (kwrooijen/font-size-set (+ (face-attribute 'default :height) kwrooijen/font-step-size)))

(defun kwrooijen/font-size-decrease ()
  "Decrease global font size."
  (interactive)
  (kwrooijen/font-size-set (- (face-attribute 'default :height) kwrooijen/font-step-size)))

(defun kwrooijen/font-size-reset ()
  "Reset global font size to default."
  (interactive)
  (kwrooijen/font-size-set kwrooijen/default-font-size))

(kwrooijen/font-size-set kwrooijen/default-font-size)

(general-define-key
 "C-x C-=" 'kwrooijen/font-size-increase
 "C-x C--" 'kwrooijen/font-size-decrease
 "C-x C-0" 'kwrooijen/font-size-reset)
#+END_SRC

* Utilities

** Hex Color Preview

#+BEGIN_SRC emacs-lisp
(defun kwrooijen/hex-colors-enable ()
  "Enable hex color preview in current buffer."
  (interactive)
  (font-lock-add-keywords
   nil
   '(("#[[:xdigit:]]\\{6\\}"
      (0 (put-text-property (match-beginning 0) (match-end 0)
                            'face (list :foreground (match-string-no-properties 0)))))))
  (font-lock-flush))

(defun kwrooijen/hex-colors-disable ()
  "Disable hex color preview in current buffer."
  (interactive)
  (font-lock-remove-keywords
   nil
   '(("#[[:xdigit:]]\\{6\\}"
      (0 (put-text-property (match-beginning 0) (match-end 0)
                            'face (list :foreground (match-string-no-properties 0)))))))
  (font-lock-flush))
#+END_SRC

* Package: Iedit :config:
:PROPERTIES:
:PACKAGE: iedit
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face! 'iedit-occurrence 'kwrooijen/salient-bg-face)
(set-face-attribute 'iedit-occurrence nil :foreground (kwrooijen-color :strong))

(advice-add 'iedit-next-occurrence :after #'kwrooijen/recenter)
(advice-add 'iedit-prev-occurrence :after #'kwrooijen/recenter)
#+END_SRC

* Package: Magit :config:
:PROPERTIES:
:PACKAGE: magit
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face!
 'magit-diff-context-highlight    'kwrooijen/darken-bg-face
 'magit-section-highlight         'kwrooijen/highlight-bg-face
 'magit-diff-hunk-heading-highlight 'kwrooijen/background-bg-face
 'magit-diff-hunk-heading         'kwrooijen/background-bg-face)
#+END_SRC

* Package: Org :config:
:PROPERTIES:
:PACKAGE: org
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
;; Block styling
(kwrooijen/reset-inherit-face!
 'org-block            'kwrooijen/darken-bg-face
 'org-quote            'kwrooijen/darken-bg-face
 'org-block-begin-line 'kwrooijen/faded-darken-fb-face
 'org-block-end-line   'kwrooijen/faded-darken-fb-face)

;; Faded faces (de-emphasized)
(kwrooijen/reset-inherit-face!
 'org-agenda-clocking          'kwrooijen/faded-fg-face
 'org-agenda-column-dateline   'kwrooijen/faded-fg-face
 'org-agenda-date-weekend      'kwrooijen/faded-fg-face
 'org-agenda-diary             'kwrooijen/faded-fg-face
 'org-agenda-dimmed-todo-face  'kwrooijen/faded-fg-face
 'org-agenda-done              'kwrooijen/faded-fg-face
 'org-agenda-filter-category   'kwrooijen/faded-fg-face
 'org-agenda-filter-effort     'kwrooijen/faded-fg-face
 'org-agenda-filter-regexp     'kwrooijen/faded-fg-face
 'org-agenda-filter-tags       'kwrooijen/faded-fg-face
 'org-agenda-restriction-lock  'kwrooijen/faded-fg-face
 'org-archived                 'kwrooijen/faded-fg-face
 'org-checkbox                 'kwrooijen/faded-fg-face
 'org-checkbox-statistics-done 'kwrooijen/faded-fg-face
 'org-checkbox-statistics-todo 'kwrooijen/faded-fg-face
 'org-clock-overlay            'kwrooijen/faded-fg-face
 'org-column                   'kwrooijen/faded-fg-face
 'org-column-title             'kwrooijen/faded-fg-face
 'org-date                     'kwrooijen/faded-fg-face
 'org-date-selected            'kwrooijen/faded-fg-face
 'org-default                  'kwrooijen/faded-fg-face
 'org-document-info            'kwrooijen/faded-fg-face
 'org-document-info-keyword    'kwrooijen/faded-fg-face
 'org-document-title           'kwrooijen/faded-fg-face
 'org-drawer                   'kwrooijen/faded-fg-face
 'org-ellipsis                 'kwrooijen/faded-fg-face
 'org-footnote                 'kwrooijen/faded-fg-face
 'org-formula                  'kwrooijen/faded-fg-face
 'org-headline-done            'kwrooijen/faded-fg-face
 'org-latex-and-related        'kwrooijen/faded-fg-face
 'org-list-dt                  'kwrooijen/faded-fg-face
 'org-macro                    'kwrooijen/faded-fg-face
 'org-meta-line                'kwrooijen/faded-fg-face
 'org-mode-line-clock          'kwrooijen/faded-fg-face
 'org-mode-line-clock-overrun  'kwrooijen/faded-fg-face
 'org-property-value           'kwrooijen/faded-fg-face
 'org-scheduled                'kwrooijen/faded-fg-face
 'org-scheduled-previously     'kwrooijen/faded-fg-face
 'org-sexp-date                'kwrooijen/faded-fg-face
 'org-special-keyword          'kwrooijen/faded-fg-face
 'org-tag-group                'kwrooijen/faded-fg-face
 'org-target                   'kwrooijen/faded-fg-face
 'org-time-grid                'kwrooijen/faded-fg-face
 'org-verse                    'kwrooijen/faded-fg-face)

;; Foreground faces (normal text)
(kwrooijen/reset-inherit-face!
 'org-agenda-calendar-event 'kwrooijen/foreground-fg-face
 'org-done                  'kwrooijen/foreground-fg-face
 'org-level-1               'kwrooijen/foreground-fg-face
 'org-level-2               'kwrooijen/foreground-fg-face
 'org-level-3               'kwrooijen/foreground-fg-face
 'org-level-4               'kwrooijen/foreground-fg-face
 'org-level-5               'kwrooijen/foreground-fg-face
 'org-level-6               'kwrooijen/foreground-fg-face
 'org-level-7               'kwrooijen/foreground-fg-face
 'org-level-8               'kwrooijen/foreground-fg-face
 'org-upcoming-deadline     'kwrooijen/foreground-fg-face)

;; Popout faces (attention-grabbing)
(kwrooijen/reset-inherit-face!
 'org-tag      'kwrooijen/popout-fg-face
 'org-verbatim 'kwrooijen/popout-fg-face
 'org-warning  'kwrooijen/popout-fg-face)

;; Strong faces (emphasis)
(kwrooijen/reset-inherit-face!
 'org-agenda-current-time 'kwrooijen/strong-fg-face
 'org-agenda-structure    'kwrooijen/strong-fg-face)

;; Salient faces (accent)
(kwrooijen/reset-inherit-face!
 'org-code     'kwrooijen/salient-fg-face
 'org-link     'kwrooijen/salient-fg-face
 'org-table    'kwrooijen/salient-fg-face
 'org-todo     'kwrooijen/salient-fg-face)

;; Error faces
(kwrooijen/reset-inherit-face!
 'org-priority 'kwrooijen/error-fg-face)
#+END_SRC

* Package: Org Agenda :config:
:PROPERTIES:
:PACKAGE: org-agenda
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face!
 'org-agenda-calendar-sexp 'kwrooijen/salient-fg-face
 'org-agenda-date          'kwrooijen/salient-fg-face
 'org-agenda-clocking      'kwrooijen/warning-fg-face)
#+END_SRC

* Package: Org Habit :config:
:PROPERTIES:
:PACKAGE: org-habit
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face!
 'org-habit-clear-future-face    'kwrooijen/salient-fb-face
 'org-habit-alert-face           'kwrooijen/warning-fb-face
 'org-habit-overdue-future-face  'kwrooijen/error-fb-face)
#+END_SRC

* Package: Paren :config:
:PROPERTIES:
:PACKAGE: paren
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face!
 'show-paren-match            'kwrooijen/salient-fg-face
 'show-paren-mismatch         'kwrooijen/error-fg-face
 'show-paren-match-expression 'kwrooijen/popout-fg-face)
#+END_SRC

* Package: Web Mode :config:
:PROPERTIES:
:PACKAGE: web-mode
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face!
 'web-mode-string-face                    'font-lock-string-face
 'web-mode-html-attr-value-face           'font-lock-string-face
 'web-mode-symbol-face                    'font-lock-keyword-face
 'web-mode-builtin-face                   'kwrooijen/strong-fg-face
 'web-mode-variable-name-face             'font-lock-variable-name-face
 'web-mode-current-element-highlight-face 'underline)
#+END_SRC

** Init :init:

#+BEGIN_SRC emacs-lisp
(add-to-list 'auto-mode-alist '("\\.html\\'" . web-mode))
#+END_SRC

* Package: Evil Highlight Persist :config:
:PROPERTIES:
:PACKAGE: evil-search-highlight-persist
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face! 'evil-search-highlight-persist-highlight-face 'highlight)
#+END_SRC

* Package: HL Line :config:
:PROPERTIES:
:PACKAGE: hl-line
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face! 'hl-line 'kwrooijen/highlight-bg-face)
#+END_SRC

* Package: Anzu :config:
:PROPERTIES:
:PACKAGE: anzu
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(set-face-attribute 'anzu-mode-line nil
                    :foreground (kwrooijen-color :salient))
#+END_SRC

* Package: Diredfl :config:
:PROPERTIES:
:PACKAGE: diredfl
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face!
 'diredfl-file-name              'kwrooijen/foreground-fg-face
 'diredfl-file-suffix            'kwrooijen/foreground-fg-face
 'diredfl-dir-name               'kwrooijen/salient-fg-face
 'diredfl-dir-heading            'kwrooijen/salient-fg-face
 'diredfl-dir-priv               'kwrooijen/subtle-fg-face
 'diredfl-date-time              'kwrooijen/faded-fg-face
 'diredfl-read-priv              'kwrooijen/subtle-fg-face
 'diredfl-write-priv             'kwrooijen/subtle-fg-face
 'diredfl-exec-priv              'kwrooijen/subtle-fg-face
 'diredfl-number                 'kwrooijen/subtle-fg-face
 'diredfl-no-priv                'kwrooijen/subtle-fg-face
 'diredfl-other-priv             'kwrooijen/subtle-fg-face
 'diredfl-link-priv              'kwrooijen/subtle-fg-face
 'diredfl-symlink                'kwrooijen/faded-fg-face
 'diredfl-compressed-file-name   'kwrooijen/faded-fg-face
 'diredfl-compressed-file-suffix 'kwrooijen/faded-fg-face)

(kwrooijen/reset-face-attributes 'diredfl-ignored-file-name)
#+END_SRC

* Package: Ediff :config:
:PROPERTIES:
:PACKAGE: ediff
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face!
 'ediff-even-diff-A 'default
 'ediff-even-diff-B 'default
 'ediff-odd-diff-A  'org-warning
 'ediff-odd-diff-B  'org-warning)
#+END_SRC

* Package: Pulse :config:
:PROPERTIES:
:PACKAGE: pulse
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(kwrooijen/reset-inherit-face!
 'pulse-highlight-face       'kwrooijen/salient-bg-face
 'pulse-highlight-start-face 'kwrooijen/salient-bg-face)
#+END_SRC

* Package: Shr :config:
:PROPERTIES:
:PACKAGE: shr
:DEFER: t
:END:

#+BEGIN_SRC emacs-lisp
(set-face-attribute 'shr-text nil
                    :family kwrooijen/font
                    :weight 'light)
#+END_SRC
